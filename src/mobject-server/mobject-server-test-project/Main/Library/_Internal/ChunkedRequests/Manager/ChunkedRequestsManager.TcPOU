<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="ChunkedRequestsManager" Id="{d60679ab-47a7-4db9-808f-1b091389a559}" SpecialFunc="None">
    <Declaration><![CDATA[{attribute 'linkalways'} 
{attribute 'no_explicit_call' := 'This FB is a CLASS and must be accessed using methods or properties'}
{attribute 'enable_dynamic_creation'}
FUNCTION_BLOCK ChunkedRequestsManager IMPLEMENTS I_ChunkedRequestsManager
VAR
	maximumChunkSizeInBytes : UDINT;
	sessionStorage : I_SessionStorage;
	config : I_ChunkedRequestsManagerConfig;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Folder Name="Constructor" Id="{787a67cf-dc0a-443d-9817-3b18a2ceda27}" />
    <Folder Name="Private" Id="{acfb1dfb-c887-48ca-b1ff-66b7f833e05a}" />
    <Method Name="CreateChunkedRequestData" Id="{6b885866-582d-4bb7-88c4-4a0bbe21e20d}" FolderPath="Private\">
      <Declaration><![CDATA[METHOD PRIVATE CreateChunkedRequestData : I_ChunkedRequestData
VAR_INPUT
	RequestChunkSizeInBytes : UDINT;
	RequestChunkCount : UDINT;
END_VAR
VAR
	pChunkedRequestData : POINTER TO ChunkedRequestData;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[pChunkedRequestData := __NEW(ChunkedRequestData(ChunkSizeInBytes := RequestChunkSizeInBytes, ChunkCount := RequestChunkCount));
CreateChunkedRequestData := pChunkedRequestData^;]]></ST>
      </Implementation>
    </Method>
    <Method Name="FB_init" Id="{7cb4f61c-1081-4781-b9bf-cf2049838078}" FolderPath="Constructor\">
      <Declaration><![CDATA[METHOD FB_init : BOOL
VAR_INPUT
	bInitRetains : BOOL; // if TRUE, the retain variables are initialized (warm start / cold start)
	bInCopyCode : BOOL;  // if TRUE, the instance afterwards gets moved into the copy code (online change)
	MaximumChunkSizeInBytes : UDINT;
	SessionStorage : I_SessionStorage;
	Config : I_ChunkedRequestsManagerConfig;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.maximumChunkSizeInBytes := MaximumChunkSizeInBytes;
THIS^.sessionStorage := SessionStorage;
THIS^.config := Config;]]></ST>
      </Implementation>
    </Method>
    <Method Name="TryHandleIncomingRequest" Id="{e2f4624f-76bc-46b3-aab8-c4659f29cdb0}">
      <Declaration><![CDATA[METHOD PUBLIC TryHandleIncomingRequest : BOOL
VAR_INPUT
	Request : I_Request;
	Response : I_ServerResponse;
	NextMidleware : I_Middleware;
END_VAR
VAR 
	hasChunkSequenceHeader : BOOL;
	chunkSequence : DINT;
	hasChunkTotalHeader : BOOL;
	chunkTotal : DINT;
	hasMoreChunksHeader : BOOL;
	hasMoreChunks : BOOL;
	requestId : T_MAXSTRING;
	sessionId : T_MAXSTRING;
	sessionData : I_SessionData;
	chunkedRequestData : I_ChunkedRequestData;
	chunkedRequest : ChunkedRequest(Request, chunkedRequestData);
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[hasChunkSequenceHeader := config.TryGetChunkSequenceFromRequest(Request, chunkSequence);
hasChunkTotalHeader := config.TryGetChunkTotalFromRequest(Request, chunkTotal);
hasMoreChunksHeader := config.TryGetHasMoreChunksFromRequest(Request, hasMoreChunks);

IF (NOT hasChunkSequenceHeader) AND (NOT hasChunkTotalHeader) AND (NOT hasMoreChunksHeader) THEN
	// this is not a chunked request, simply return and it will be handled by the next middleware automatically.
	RETURN;
END_IF

// when reaching here we know that the request has at least one of the headers associated with chunked requests
// from this point on, we simply validate until we know we have all the required parts. 

IF NOT hasChunkSequenceHeader THEN
	response.Error('Chunked Request found, but chunk sequence header data is missing');
	TryHandleIncomingRequest := TRUE;
	RETURN;
END_IF

IF chunkSequence = 0 THEN
	response.Error('Chunk sequence 0 received, chunks must start from 1');
	TryHandleIncomingRequest := TRUE;
	RETURN;
END_IF

IF NOT hasChunkTotalHeader THEN
	response.Error('Chunked Request found, but chunk total header data is missing');
	TryHandleIncomingRequest := TRUE;
	RETURN;
END_IF

IF chunkSequence > chunkTotal THEN
	response.Error('Chunk sequence is greater than the chunk total');
	TryHandleIncomingRequest := TRUE;
	RETURN;
END_IF

IF NOT hasMoreChunksHeader THEN
	response.Error('Chunked Request found, but has more chunks header data is missing');
	TryHandleIncomingRequest := TRUE;
	RETURN;
END_IF

IF NOT config.TryGetRequestIdFromRequest(Request, requestId) THEN
	response.Error('Chunked Request found, but request id header data is missing');
	TryHandleIncomingRequest := TRUE;
	RETURN;
END_IF

IF NOT config.TryGetSessionIdFromRequest(Request, sessionId) THEN
	response.Error('Chunked Request found, but session id header data is missing');
	TryHandleIncomingRequest := TRUE;
	RETURN;
END_IF

// at this point we know we have all of the data required for handling a chunked request

IF NOT sessionStorage.TryRetreiveSessionDataById(sessionId, requestId, sessionData) THEN	
	chunkedRequestData := CreateChunkedRequestData(maximumChunkSizeInBytes,chunkTotal);
ELSIF NOT __QUERYINTERFACE(sessionData, chunkedRequestData) THEN
	response.Error('Server was unable to retreive previous session data as chunked request data');
	TryHandleIncomingRequest := TRUE;
	RETURN;
END_IF

chunkedRequestData.TryAddChunkedRequest(request, chunkSequence);

IF NOT chunkedRequestData.IsComplete THEN
	Response.Accepted();
	TryHandleIncomingRequest := TRUE;
	RETURN;
END_IF

NextMidleware.Handle(chunkedRequest, Response);
// delete chunk?
TryHandleIncomingRequest := TRUE;]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>