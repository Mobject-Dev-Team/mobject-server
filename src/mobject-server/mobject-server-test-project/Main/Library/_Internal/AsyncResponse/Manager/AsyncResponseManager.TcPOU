<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="AsyncResponseManager" Id="{227d2639-d4f9-47c2-ab56-861a5448ae71}" SpecialFunc="None">
    <Declaration><![CDATA[{attribute 'linkalways'} 
{attribute 'no_explicit_call' := 'This FB is a CLASS and must be accessed using methods or properties'}
{attribute 'enable_dynamic_creation'}
FUNCTION_BLOCK AsyncResponseManager IMPLEMENTS I_AsyncResponseManager
VAR
	sessionStorage : I_SessionStorage;
	responseIdGenerator : I_ResponseIdGenerator;
	headerAccessor : I_AsyncResponseHeaderAccessor;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Folder Name="Constructor" Id="{512c79a0-d7ea-4632-8454-ff0bdda61950}" />
    <Folder Name="Protected" Id="{10f5e310-740f-44f6-88d4-6719d1d292b2}" />
    <Method Name="CreateAsyncResponseSessionData" Id="{9309a56c-b4a3-406f-ba56-d711c30ffecf}" FolderPath="Protected\">
      <Declaration><![CDATA[METHOD PROTECTED CreateAsyncResponseSessionData : I_AsyncResponseSessionData
VAR_INPUT
	RetryDelayInMs : UDINT;
END_VAR
VAR
	pAsyncResponseSessionData : POINTER TO AsyncResponseSessionData;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[pAsyncResponseSessionData := __NEW(AsyncResponseSessionData(SuggestedRetryDelayInMs := RetryDelayInMs));
CreateAsyncResponseSessionData := pAsyncResponseSessionData^;]]></ST>
      </Implementation>
    </Method>
    <Method Name="CreateAsyncResponseSessionDataProxy" Id="{1c5a207a-cefc-43c2-99ac-d1b15f0cb591}" FolderPath="Protected\">
      <Declaration><![CDATA[METHOD PROTECTED CreateAsyncResponseSessionDataProxy : I_AsyncResponseSessionData
VAR_INPUT
	ProxySessionId : T_MAXSTRING;
	ProxyResponseId : T_MAXSTRING;
END_VAR
VAR
	pAsyncResponseSessionDataProxy : POINTER TO AsyncResponseSessionDataProxy;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[pAsyncResponseSessionDataProxy := __NEW(AsyncResponseSessionDataProxy(SessionStorage := THIS^.sessionStorage, SessionId := ProxySessionId, ResponseId := ProxyResponseId));
CreateAsyncResponseSessionDataProxy := pAsyncResponseSessionDataProxy^;]]></ST>
      </Implementation>
    </Method>
    <Method Name="FB_init" Id="{10c5352a-129b-4eaa-abe3-146411036763}" FolderPath="Constructor\">
      <Declaration><![CDATA[METHOD FB_init : BOOL
VAR_INPUT
	bInitRetains : BOOL; // if TRUE, the retain variables are initialized (warm start / cold start)
	bInCopyCode : BOOL;  // if TRUE, the instance afterwards gets moved into the copy code (online change)
	SessionStorage : I_SessionStorage;
	ResponseIdGenerator : I_ResponseIdGenerator;
	HeaderAccessor : I_AsyncResponseHeaderAccessor;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.sessionStorage := SessionStorage;
THIS^.responseIdGenerator := ResponseIdGenerator;
THIS^.headerAccessor := HeaderAccessor;]]></ST>
      </Implementation>
    </Method>
    <Method Name="TryConvertResponseToAsyncResponseSessionData" Id="{9b1dd38d-7371-4b2c-aeed-2ce787286b24}">
      <Declaration><![CDATA[METHOD TryConvertResponseToAsyncResponseSessionData : BOOL
VAR_INPUT
	Response : I_ServerResponse;
	RetryDelayInMs : UDINT;
	Destination : REFERENCE TO I_AsyncResponseSessionData;
END_VAR
VAR
	sessionId : T_MAXSTRING;
	responseId : T_MAXSTRING;
	newResponseId : T_MAXSTRING;
	asyncResponseSessionData : I_AsyncResponseSessionData;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT headerAccessor.TryGetSessionIdFromResponse(Response,sessionId) THEN
	Response.Error('Unable to create a pending response without a sessionId');
	RETURN;
END_IF

asyncResponseSessionData := CreateAsyncResponseSessionData(RetryDelayInMs);
responseId := responseIdGenerator.Generate();

IF NOT sessionStorage.TryStoreSessionDataById(sessionId, responseId, asyncResponseSessionData) THEN
	Response.Error('Unable to store pending response');
	asyncResponseSessionData.Dispose();
	RETURN;
END_IF

Response.Pending(responseId, RetryDelayInMs);
Destination := CreateAsyncResponseSessionDataProxy(sessionId, responseId);
TryConvertResponseToAsyncResponseSessionData := true;]]></ST>
      </Implementation>
    </Method>
    <Method Name="TryHandleIncomingRequest" Id="{5dd32684-e554-429c-8b1d-ad94c31a0043}">
      <Declaration><![CDATA[METHOD PUBLIC TryHandleIncomingRequest : BOOL
VAR_INPUT
	Request : I_Request;
	Response : I_ServerResponse;
END_VAR
VAR
	responseId : T_MAXSTRING;
	sessionId : T_MAXSTRING;
	sessionData : I_SessionData;
	asyncResponseData : I_AsyncResponseSessionData;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT headerAccessor.TryGetResponseIdFromRequest(Request, responseId) THEN
	RETURN;
END_IF

IF NOT headerAccessor.TryGetSessionIdFromRequest(Request, sessionId) THEN
	RETURN;
END_IF

IF NOT sessionStorage.TryRetreiveSessionDataById(sessionId, responseId, sessionData) THEN
	RETURN;
END_IF

IF NOT __QUERYINTERFACE(sessionData, asyncResponseData) THEN
	RETURN;
END_IF

IF asyncResponseData.IsPending THEN
	Response.Pending(responseId, asyncResponseData.SuggestedRetryDelayInMs);
	TryHandleIncomingRequest := TRUE;
	RETURN;
END_IF

IF NOT asyncResponseData.TryUpdateServerResponse(Response) THEN	
	Response.Error('Unable to update server response with pending data');
	TryHandleIncomingRequest := TRUE;
	RETURN;
END_IF

sessionStorage.DisposeSessionDataById(sessionId, responseId);
TryHandleIncomingRequest := TRUE;]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>